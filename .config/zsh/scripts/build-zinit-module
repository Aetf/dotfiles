# This is to workaround zinit-module build issue on macos
# See https://github.com/zdharma-continuum/zinit-module/issues/3

ZINIT_MODULE_URL=https://github.com/zdharma-continuum/zinit-module.git

# The following is copied from `zinit module build` with modifications to
# apply zsh 5.8.1 patch and macos CFLAGS modifications.
.zinit-build-module() {
    setopt localoptions localtraps
    trap 'return 1' INT TERM
    if command git -C "${ZINIT[MODULE_DIR]}" rev-parse 2>/dev/null; then
        command git -C "${ZINIT[MODULE_DIR]}" clean -d -f -f
        command git -C "${ZINIT[MODULE_DIR]}" reset --hard HEAD
        command git -C "${ZINIT[MODULE_DIR]}" pull
    else
        command git clone "$ZINIT_MODULE_URL" "${ZINIT[MODULE_DIR]}" || {
            builtin print "${ZINIT[col-error]}Failed to clone module repo${ZINIT[col-rst]}"
            return 1
        }
    fi

    # zsh 5.8.1 compatibility
    +zinit-message "{pname}== Applying zsh 5.8.1 patches to zdharma-continuum/zinit-module =={rst}"
    autoload -z is-at-least
    if is-at-least 5.8.1; then
      git -C "${ZINIT[MODULE_DIR]}" apply "$ZDOTDIR/scripts/0001-zsh-5.8.1-compatibility.patch"
    fi
    CFLAGS="-g -Wall -O3"
    if ! is-at-least 5.9; then
        CFLAGS="-Wno-implicit-function-declaration $CFLAGS"
    fi

    ( builtin cd -q "${ZINIT[MODULE_DIR]}"
      +zinit-message "{pname}== Building module zdharma-continuum/zinit-module, running: make clean, then ./configure and then make =={rst}"
      +zinit-message "{pname}== The module sources are located at: "${ZINIT[MODULE_DIR]}" =={rst}"
      if [[ -f Makefile ]] {
          if [[ "$1" = "--clean" ]] {
              noglob +zinit-message {p}-- make distclean --{rst}
              make distclean
              ((1))
          } else {
              noglob +zinit-message {p}-- make clean --{rst}
              make clean
          }
      }
      noglob +zinit-message  {p}-- ./configure --{rst}

      CPPFLAGS=-I/usr/local/include CFLAGS="$CFLAGS" LDFLAGS=-L/usr/local/lib ./configure --disable-gdbm --without-tcsetpgrp && {
          noglob +zinit-message {p}-- make --{rst}
          if { make } {
            [[ -f Src/zdharma_continuum/zinit.so ]] && cp -vf Src/zdharma_continuum/zinit.{so,bundle}
            noglob +zinit-message "{info}Module has been built correctly.{rst}"
            .zinit-module info
          } else {
              noglob +zinit-message  "{error}Module didn't build.{rst} "
              .zinit-module info --link
          }
      }
      builtin print $EPOCHSECONDS >! "${ZINIT[MAN_DIR]}/COMPILED_AT"
    )
}
